buildscript {
    repositories {
        jcenter()
        maven {
            url "https://dl.bintray.com/ngbinh/maven/"
        }
    }
    dependencies {
        classpath 'org.github.ngbinh.scalastyle:gradle-scalastyle-plugin_2.12:1.0.0'
        classpath "net.saliman:gradle-cobertura-plugin:2.5.4"
        classpath "org.kt3k.gradle.plugin:coveralls-gradle-plugin:2.8.2"
    }
}

allprojects {
    repositories {
        maven {
            url "https://dl.bintray.com/ngbinh/maven/"
        }
        jcenter()
    }
}

def scalaMajorVersion = "2.12"
def scalaVersion = "${scalaMajorVersion}.4"
def akkaVersion = "2.5.9"

subprojects {
    apply plugin: 'scala'
    apply plugin: 'scalaStyle'
    apply plugin: 'net.saliman.cobertura'
    apply plugin: 'com.github.kt3k.coveralls'

    dependencies {
        // scala
        compile "org.scala-lang:scala-library:${scalaVersion}"
        compile "org.scala-lang:scala-compiler:${scalaVersion}"

        // logger
        compile "ch.qos.logback:logback-classic:1.2.3"
        compile "com.typesafe.scala-logging:scala-logging_${scalaMajorVersion}:3.7.2"

        // akka
        compile "com.typesafe.akka:akka-cluster_${scalaMajorVersion}:${akkaVersion}"
        compile "com.typesafe.akka:akka-slf4j_${scalaMajorVersion}:${akkaVersion}"

        testCompile "org.scalatest:scalatest_${scalaMajorVersion}:3.0.4"
    }

    scalaStyle {
        configLocation = "$rootDir/scalastyle_config.xml"
        includeTestSourceDirectory = true
        failOnWarning = true
        source = "src/main/scala"
        testSource = "src/test/scala"
    }

    tasks.withType(JavaExec) {
        systemProperties System.properties
    }

    cobertura.coverageFormats = ['html', 'xml']
}

project(":master") {
    apply plugin: 'application'

    mainClassName = "com.github.jaitl.cloud.master.App"

    dependencies {
        compile project(":base-worker")

        compile "com.typesafe.akka:akka-cluster-tools_${scalaMajorVersion}:${akkaVersion}"
        compile "com.typesafe.akka:akka-cluster-sharding_${scalaMajorVersion}:${akkaVersion}"
    }
}

project(":base-worker") {
    dependencies {
        compile "com.typesafe.akka:akka-cluster-tools_${scalaMajorVersion}:${akkaVersion}"
    }
}

project(":simple-worker") {
    apply plugin: 'application'

    mainClassName = "com.github.jaitl.cloud.simple.worker.App"

    dependencies {
        compile project(":base-worker")

    }
}
