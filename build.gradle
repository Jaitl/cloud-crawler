buildscript {
    repositories {
        jcenter()
        maven {
            url "https://dl.bintray.com/ngbinh/maven/"
        }
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
    dependencies {
        classpath 'org.github.ngbinh.scalastyle:gradle-scalastyle-plugin_2.12:1.0.0'
        classpath "net.saliman:gradle-cobertura-plugin:2.5.4"
        classpath "org.kt3k.gradle.plugin:coveralls-gradle-plugin:2.8.2"
        classpath "gradle.plugin.com.github.maiflai:gradle-scalatest:0.19"
    }
}

allprojects {
    repositories {
        maven {
            url "https://dl.bintray.com/ngbinh/maven/"
        }
        maven {
            url "https://plugins.gradle.org/m2/"
        }
        jcenter()
    }
}

def scalaMajorVersion = "2.12"
def scalaVersion = "${scalaMajorVersion}.5"
def akkaVersion = "2.5.9"

subprojects {
    apply plugin: 'scala'
    apply plugin: 'scalaStyle'
    apply plugin: 'jacoco'
    apply plugin: 'com.github.kt3k.coveralls'
    apply plugin: "com.github.maiflai.scalatest"

    dependencies {
        // scala
        compile "org.scala-lang:scala-library:${scalaVersion}"
        compile "org.scala-lang:scala-compiler:${scalaVersion}"

        // logger
        compile "ch.qos.logback:logback-classic:1.2.3"
        compile "com.typesafe.scala-logging:scala-logging_${scalaMajorVersion}:3.7.2"

        // test
        testCompile "org.scalatest:scalatest_${scalaMajorVersion}:3.0.4"
        testRuntime "org.pegdown:pegdown:1.1.0"

    }

    scalaStyle {
        configLocation = "$rootDir/scalastyle_config.xml"
        includeTestSourceDirectory = false
        failOnWarning = true
        source = "src/main/scala"
        testSource = "src/test/scala"
    }

    tasks.withType(JavaExec) {
        systemProperties System.properties
    }

    jacocoTestReport {
        reports {
            xml.enabled = true
            html.enabled = true
        }
    }

    tasks.withType(ScalaCompile) {
        scalaCompileOptions.additionalParameters = ["-unchecked", "-deprecation", "-feature", "-Xfatal-warnings"]
    }
}

project(":models") {

}

project(":worker") {
    dependencies {
        compile project(":models")

        // akka
        compile "com.typesafe.akka:akka-cluster_${scalaMajorVersion}:${akkaVersion}"
        compile "com.typesafe.akka:akka-slf4j_${scalaMajorVersion}:${akkaVersion}"
        compile "com.typesafe.akka:akka-cluster-tools_${scalaMajorVersion}:${akkaVersion}"

        compile "org.mongodb.scala:mongo-scala-driver_${scalaMajorVersion}:2.2.1"
        compile "org.asynchttpclient:async-http-client:2.4.7"

        // test
        testCompile "com.typesafe.akka:akka-testkit_${scalaMajorVersion}:${akkaVersion}"
        testCompile "org.scalamock:scalamock_${scalaMajorVersion}:4.1.0"

    }
}

project(":master") {
    apply plugin: 'application'

    mainClassName = "com.github.jaitl.crawler.master.MasterApp"
    dependencies {
        compile project(":models")

        // akka
        compile "com.typesafe.akka:akka-cluster_${scalaMajorVersion}:${akkaVersion}"
        compile "com.typesafe.akka:akka-slf4j_${scalaMajorVersion}:${akkaVersion}"
        compile "com.typesafe.akka:akka-cluster-tools_${scalaMajorVersion}:${akkaVersion}"
        compile "com.typesafe.akka:akka-cluster-sharding_${scalaMajorVersion}:${akkaVersion}"
        compile "org.mongodb.scala:mongo-scala-driver_${scalaMajorVersion}:2.2.1"

        // test
        testCompile "com.typesafe.akka:akka-testkit_${scalaMajorVersion}:${akkaVersion}"
        testCompile "org.scalamock:scalamock_${scalaMajorVersion}:4.1.0"
    }
}

project(":simple-worker") {
    apply plugin: 'application'

    mainClassName = "com.github.jaitl.cloud.simple.worker.App"

    dependencies {
        compile project(":worker")
    }
}
